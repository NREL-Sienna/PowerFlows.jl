"""
    struct ACPowerFlowJacobian

A struct that represents the Jacobian matrix for AC power flow calculations.

This struct uses the functor pattern, meaning instances of `ACPowerFlowJacobian` store the data (Jacobian matrix) internally
and can be called as a function at the same time. Calling the instance as a function updates the stored Jacobian matrix.

# Fields
- `data::ACPowerFlowData`: The grid model data used for power flow calculations.
- `Jv::SparseArrays.SparseMatrixCSC{Float64, $J_INDEX_TYPE}`: The Jacobian matrix, which is updated by `_update_jacobian_matrix_values!`.
- `bus_slack_participation_factors::SparseVector{Float64, Int}`: Normalized per-bus slack participation factors for the current time step (from the `ACPowerFlowResidual`). Used for the distributed slack Jacobian entries.
- `subnetworks::Dict{Int64, Vector{Int64}}`: Subnetwork mapping from REF bus to bus list (from the `ACPowerFlowResidual`). Used for the distributed slack Jacobian entries.
- `independent_ref::Set{Int}`: Multi-swing REF bus indices, from `_multi_swing_ref_indices`. Computed once at construction because the Q-limit loop only flips PV↔PQ, never REF.
"""
struct ACPowerFlowJacobian
    data::ACPowerFlowData
    Jv::SparseArrays.SparseMatrixCSC{Float64, J_INDEX_TYPE}  # This is the Jacobian matrix, updated in place by `_update_jacobian_matrix_values!`
    bus_slack_participation_factors::SparseVector{Float64, Int}
    subnetworks::Dict{Int64, Vector{Int64}}
    independent_ref::Set{Int}
    bus_active_constant_I::Vector{Float64}
    bus_reactive_constant_I::Vector{Float64}
    bus_active_constant_Z::Vector{Float64}
    bus_reactive_constant_Z::Vector{Float64}
    # nzval-offset caches built once at construction; see _build_polar_nz_caches.
    # The fill writes directly into nonzeros(Jv) via these, avoiding setindex
    # binary search and Yb[i,j] getindex in the hot path.
    od_from::Vector{Int}            # bus_from per off-diagonal Ybus entry
    od_to::Vector{Int}              # bus_to per off-diagonal Ybus entry
    od_ybus_nz::Vector{Int}         # nonzeros(Yb) index for that entry (g, b)
    od_jnz::Matrix{Int}             # 4 × n_od: J nzval offsets for (p,vm),(q,vm),(p,va),(q,va)
    diag_jnz::Matrix{Int}           # 4 × n_buses: J nzval offsets for the self block, same slot order
    diag_ybus_nz::Vector{Int}       # n_buses: nonzeros(Yb) index for Yb[i,i]
    diag_accum::Matrix{Float64}     # 4 × n_buses scratch for the per-bus diagonal accumulation
end

"""
    (J::ACPowerFlowJacobian)(time_step::Int64)

Update the Jacobian matrix `Jv` using `_update_jacobian_matrix_values!` and the provided data and time step.

Defining this method allows an instance of `ACPowerFlowJacobian` to be called as a function, following the functor pattern.

# Arguments
- `time_step::Int64`: The time step for the calculations.

# Example
```julia
residual = ACPowerFlowResidual(data, time_step)
J = ACPowerFlowJacobian(residual, time_step)
J(time_step)  # Updates the Jacobian matrix Jv
```
"""
function (J::ACPowerFlowJacobian)(time_step::Int64)
    _update_jacobian_matrix_values!(J.Jv, J.data, time_step,
        J.bus_slack_participation_factors, J.subnetworks, J.independent_ref,
        J.bus_active_constant_I, J.bus_reactive_constant_I,
        J.bus_active_constant_Z, J.bus_reactive_constant_Z,
        J.od_from, J.od_to, J.od_ybus_nz, J.od_jnz,
        J.diag_jnz, J.diag_ybus_nz, J.diag_accum)
    return
end

"""
    (J::ACPowerFlowJacobian)(J::SparseArrays.SparseMatrixCSC{Float64, $J_INDEX_TYPE}, time_step::Int64)

Use the `ACPowerFlowJacobian` to update the provided Jacobian matrix `J` inplace.

Update the internally stored Jacobian matrix `Jv` using `_update_jacobian_matrix_values!` and the provided data and time step, and write the updated Jacobian values to `J`.

This method allows an instance of ACPowerFlowJacobian to be called as a function, following the functor pattern.

# Arguments
- `J::SparseArrays.SparseMatrixCSC{Float64, $J_INDEX_TYPE}`: A sparse matrix to be updated with new values of the Jacobian matrix.
- `time_step::Int64`: The time step for the calculations.

# Example
```julia
residual = ACPowerFlowResidual(data, time_step)
J = ACPowerFlowJacobian(residual, time_step)
Jv = SparseArrays.sparse(Float64[], J_INDEX_TYPE[], J_INDEX_TYPE[])
J(Jv, time_step)  # Updates the Jacobian matrix Jv and writes it to J
```
"""
function (J::ACPowerFlowJacobian)(
    Jv::SparseArrays.SparseMatrixCSC{Float64, J_INDEX_TYPE},
    time_step::Int64,
)
    _update_jacobian_matrix_values!(J.Jv, J.data, time_step,
        J.bus_slack_participation_factors, J.subnetworks, J.independent_ref,
        J.bus_active_constant_I, J.bus_reactive_constant_I,
        J.bus_active_constant_Z, J.bus_reactive_constant_Z,
        J.od_from, J.od_to, J.od_ybus_nz, J.od_jnz,
        J.diag_jnz, J.diag_ybus_nz, J.diag_accum)
    copyto!(Jv, J.Jv)
    return
end

"""
    ACPowerFlowJacobian(residual::ACPowerFlowResidual, time_step::Int64) -> ACPowerFlowJacobian

Constructor for `ACPowerFlowJacobian`. The returned instance has its sparsity
pattern initialized and shares the residual's slack-participation, subnetwork,
and ZIP-coefficient caches — the residual must be constructed first against the
same `data` and `time_step`.

# Arguments
- `residual::ACPowerFlowResidual`: The companion residual; supplies `data`,
  `bus_slack_participation_factors`, `subnetworks`, and the per-bus ZIP load
  coefficient vectors.
- `time_step::Int64`: The time step for the calculations.

# Example
```julia
residual = ACPowerFlowResidual(data, time_step)
J = ACPowerFlowJacobian(residual, time_step)
J(time_step)  # Updates the Jacobian matrix stored internally in J.
J.Jv  # Access the Jacobian matrix stored internally in J.
```
"""
# Memoize the expensive Jacobian sparse-structure build (~3.2 MB on 2000 buses) so it is built
# once and reused across the Q-limit inner loop and repeated PCM solves. The structure is
# invariant under the PV→PQ flips that drive the Q-limit loop (colptr/rowval verified byte-identical
# across a flip); the cache key is the network-matrix identity + slack nonzero pattern + area
# interchange data, so a distributed-slack participant drop correctly rebuilds. Returns a full `copy`
# so each `ACPowerFlowJacobian` owns a fresh mutable buffer, or `nothing` to signal a rebuild. Lives
# in its own `data.ac_jacobian_structure_cache` field ([`ACJacobianStructureCache`](@ref)) so it
# never collides with the FastDecoupled/DC caches in `data.solver_cache[]`.
_reuse_ac_jac_structure(::Nothing, matrix, nzind, area_data) = nothing
_reuse_ac_jac_structure(e::ACJacobianStructureCache, matrix, nzind, area_data) =
    if e.matrix === matrix && e.nzind == nzind && e.area_data === area_data
        copy(e.structure)
    else
        nothing
    end

function _get_or_build_jacobian_structure(
    data::ACPowerFlowData,
    slack_factors::SparseVector{Float64, Int},
    subnetworks::Dict{Int64, Vector{Int64}},
    time_step::Int64,
)
    nzind = SparseArrays.nonzeroinds(slack_factors)
    reused = _reuse_ac_jac_structure(
        data.ac_jacobian_structure_cache[], data.power_network_matrix, nzind,
        data.area_interchange)
    isnothing(reused) || return reused
    Jv0 = _create_jacobian_matrix_structure(data, slack_factors, subnetworks, time_step)
    # Cache a pristine copy; `Jv0` is about to be mutated by the Newton loop. `area_data`
    # is stored by IDENTITY (not copied) — a rebuilt `PowerFlowData` gets a fresh
    # `AreaInterchangeData`, forcing a rebuild; a Q-limit flip keeps the same object, so
    # reuse still works.
    data.ac_jacobian_structure_cache[] =
        ACJacobianStructureCache(
            data.power_network_matrix, copy(nzind), copy(Jv0), data.area_interchange)
    return Jv0
end

function ACPowerFlowJacobian(
    residual::ACPowerFlowResidual,
    time_step::Int64,
)
    Jv0 = _get_or_build_jacobian_structure(
        residual.data,
        residual.bus_slack_participation_factors,
        residual.subnetworks,
        time_step,
    )
    od_from, od_to, od_ybus_nz, od_jnz, diag_jnz, diag_ybus_nz, diag_accum =
        _build_polar_nz_caches(residual.data, Jv0)
    return ACPowerFlowJacobian(
        residual.data,
        Jv0,
        residual.bus_slack_participation_factors,
        residual.subnetworks,
        _multi_swing_ref_indices(residual.data.bus_type, residual.subnetworks, time_step),
        residual.bus_active_constant_I,
        residual.bus_reactive_constant_I,
        residual.bus_active_constant_Z,
        residual.bus_reactive_constant_Z,
        od_from,
        od_to,
        od_ybus_nz,
        od_jnz,
        diag_jnz,
        diag_ybus_nz,
        diag_accum,
    )
end

"""
Build the once-per-construction nzval-offset caches that drive the polar
Jacobian fill. The Ybus is swept in CSC column order (column = `bus_to`,
row = `bus_from`), mirroring the residual sweep so the stored Ybus nonzero is
exactly `Yb[bus_from, bus_to]`. For each off-diagonal entry we record the four
`J.Jv` nzval offsets of its 2×2 block; for each diagonal we record the self
block offsets and the `Yb[i,i]` position. `diag_accum` is the 4×n_buses scratch
the fill uses to accumulate the per-bus diagonal terms across the column sweep
(the accumulation order differs from the old Set iteration, hence values match
only up to floating-point reassociation).
"""
function _build_polar_nz_caches(
    data::ACPowerFlowData,
    Jv::SparseArrays.SparseMatrixCSC{Float64, J_INDEX_TYPE},
)
    Yb = data.power_network_matrix.data
    num_buses = first(size(data.bus_type))
    Yrows = SparseArrays.rowvals(Yb)
    n_od = 0
    for bus_to in 1:num_buses
        for j in SparseArrays.nzrange(Yb, bus_to)
            Yrows[j] != bus_to && (n_od += 1)
        end
    end
    od_from = Vector{Int}(undef, n_od)
    od_to = Vector{Int}(undef, n_od)
    od_ybus_nz = Vector{Int}(undef, n_od)
    od_jnz = Matrix{Int}(undef, 4, n_od)
    diag_jnz = Matrix{Int}(undef, 4, num_buses)
    diag_ybus_nz = zeros(Int, num_buses)  # 0 = no Yb[i,i] nonzero (self-admittance ≡ 0)
    # Diagonal J slots always exist (neighbors include self); fill them even if the
    # bus has no Ybus diagonal entry, matching the old getindex-returns-0 behavior.
    for bus_from in 1:num_buses
        col_vm = 2 * bus_from - 1
        col_va = 2 * bus_from
        diag_jnz[1, bus_from] = _jv_nz_index(Jv, 2 * bus_from - 1, col_vm)
        diag_jnz[2, bus_from] = _jv_nz_index(Jv, 2 * bus_from, col_vm)
        diag_jnz[3, bus_from] = _jv_nz_index(Jv, 2 * bus_from - 1, col_va)
        diag_jnz[4, bus_from] = _jv_nz_index(Jv, 2 * bus_from, col_va)
    end
    k = 0
    for bus_to in 1:num_buses
        col_to_vm = 2 * bus_to - 1
        col_to_va = 2 * bus_to
        for j in SparseArrays.nzrange(Yb, bus_to)
            bus_from = Yrows[j]
            row_from_p = 2 * bus_from - 1
            row_from_q = 2 * bus_from
            if bus_from == bus_to
                diag_ybus_nz[bus_from] = j
            else
                k += 1
                od_from[k] = bus_from
                od_to[k] = bus_to
                od_ybus_nz[k] = j
                od_jnz[1, k] = _jv_nz_index(Jv, row_from_p, col_to_vm)
                od_jnz[2, k] = _jv_nz_index(Jv, row_from_q, col_to_vm)
                od_jnz[3, k] = _jv_nz_index(Jv, row_from_p, col_to_va)
                od_jnz[4, k] = _jv_nz_index(Jv, row_from_q, col_to_va)
            end
        end
    end
    diag_accum = zeros(Float64, 4, num_buses)
    return od_from, od_to, od_ybus_nz, od_jnz, diag_jnz, diag_ybus_nz, diag_accum
end

"""
Create the Jacobian matrix structure for a reference bus (REF). Currently unused: we \
fill all four values even for PV buses with structiural zeros using the same function as for PQ buses.
"""
function _create_jacobian_matrix_structure_bus!(rows::Vector{J_INDEX_TYPE},
    columns::Vector{J_INDEX_TYPE},
    values::Vector{Float64},
    bus_from::Int,
    bus_to::Int,
    row_from_p::Int,
    row_from_q::Int,
    col_to_vm::Int,
    col_to_va::Int,
    ::Val{PSY.ACBusTypes.REF})
    if bus_from == bus_to
        # Active PF w/r Local Active Power
        push!(rows, row_from_p)
        push!(columns, col_to_vm)
        push!(values, 0.0)
        # Reactive PF w/r Local Reactive Power
        push!(rows, row_from_q)
        push!(columns, col_to_va)
        push!(values, 0.0)
    end
    return
end

"""
Create the Jacobian matrix structure for a PV bus. Currently unused: we \
fill all four values even for PV buses with structiural zeros using the same function as for PQ buses.
"""
function _create_jacobian_matrix_structure_bus!(rows::Vector{J_INDEX_TYPE},
    columns::Vector{J_INDEX_TYPE},
    values::Vector{Float64},
    bus_from::Int,
    bus_to::Int,
    row_from_p::Int,
    row_from_q::Int,
    col_to_vm::Int,
    col_to_va::Int,
    ::Val{PSY.ACBusTypes.PV})
    # Active PF w/r Voltage Angle
    push!(rows, row_from_p)
    push!(columns, col_to_va)
    push!(values, 0.0)
    # Reactive PF w/r Voltage Angle
    push!(rows, row_from_q)
    push!(columns, col_to_va)
    push!(values, 0.0)
    if bus_from == bus_to
        # Reactive PF w/r Local Reactive Power
        push!(rows, row_from_q)
        push!(columns, col_to_vm)
        push!(values, 0.0)
    end
    return
end

"""
Create the Jacobian matrix structure for a PQ bus. Using this for all buses because
    a) for REF buses it doesn't matter if there are 2 values or 4 values - there are not many of them in the grid
    b) for PV buses we fill all four values because we can have a PV -> PQ transition and then we need to fill all four values
"""
function _create_jacobian_matrix_structure_bus!(rows::Vector{J_INDEX_TYPE},
    columns::Vector{J_INDEX_TYPE},
    values::Vector{Float64},
    bus_from::Int,
    bus_to::Int,
    row_from_p::Int,
    row_from_q::Int,
    col_to_vm::Int,
    col_to_va::Int,
    # ::Val{PSY.ACBusTypes.PQ}
)
    # Active PF w/r Voltage Magnitude
    push!(rows, row_from_p)
    push!(columns, col_to_vm)
    push!(values, 0.0)
    # Reactive PF w/r Voltage Magnitude
    push!(rows, row_from_q)
    push!(columns, col_to_vm)
    push!(values, 0.0)
    # Active PF w/r Voltage Angle
    push!(rows, row_from_p)
    push!(columns, col_to_va)
    push!(values, 0.0)
    # Reactive PF w/r Voltage Angle
    push!(rows, row_from_q)
    push!(columns, col_to_va)
    push!(values, 0.0)
    return
end

"""
    _create_jacobian_matrix_structure_lcc(
        data::ACPowerFlowData,
        rows::Vector{$J_INDEX_TYPE},
        columns::Vector{$J_INDEX_TYPE},
        values::Vector{Float64},
        num_buses::Int
    )

Create the Jacobian matrix structure for LCC HVDC systems.

# Description

The function iterates over each LCC system and adds the non-zero entries to the Jacobian matrix structure.
The state vector for every LCC contains 4 variables: tap position and thyristor angle for both the rectifier and inverter sides.
The indices of non-zero entries correspond to the positions of these variables in the extended state vector.

For an LCC system connecting bus ``i`` (rectifier side) and bus ``j`` (inverter side), the state variables are:
- ``t_i``: tap position at rectifier
- ``t_j``: tap position at inverter
- ``\\alpha_i``: thyristor angle at rectifier
- ``\\alpha_j``: thyristor angle at inverter

The residuals include:
- ``F_{t_i}``: Active power balance at rectifier (controls ``P_i`` to match setpoint)
- ``F_{t_j}``: Total active power balance across LCC system
- ``F_{\\alpha_i}``: Rectifier thyristor angle constraint (maintains ``\\alpha_i`` at minimum)
- ``F_{\\alpha_j}``: Inverter thyristor angle constraint (maintains ``\\alpha_j`` at minimum)

# Example Structure

For a system with 2 buses connected by one LCC where bus 1 is the rectifier side and bus 2 is the inverter side,
the Jacobian matrix would have non-zero entries at positions like:

```math
\\begin{array}{c|cccccccc}
 & V_1 & \\delta_1 & V_2 & \\delta_2 & t_1 & t_2 & \\alpha_1 & \\alpha_2 \\\\
\\hline
P_1 & \\frac{\\partial P_1}{\\partial V_1} & & & & \\frac{\\partial P_1}{\\partial t_1} & & \\frac{\\partial P_1}{\\partial \\alpha_1} & \\\\
Q_1 & \\frac{\\partial Q_1}{\\partial V_1} & & & & \\frac{\\partial Q_1}{\\partial t_1} & & \\frac{\\partial Q_1}{\\partial \\alpha_1} & \\\\
P_2 & & & & & & & & \\\\
Q_2 & & & & & & & & \\\\
F_{t_1} & \\frac{\\partial F_{t_1}}{\\partial V_1} & & & & \\frac{\\partial F_{t_1}}{\\partial t_1} & & \\frac{\\partial F_{t_1}}{\\partial \\alpha_1} & \\\\
F_{t_2} & \\frac{\\partial F_{t_2}}{\\partial V_1} & & \\frac{\\partial F_{t_2}}{\\partial V_2} & & \\frac{\\partial F_{t_2}}{\\partial t_1} & \\frac{\\partial F_{t_2}}{\\partial t_2} & \\frac{\\partial F_{t_2}}{\\partial \\alpha_1} & \\frac{\\partial F_{t_2}}{\\partial \\alpha_2} \\\\
F_{\\alpha_1} & & & & & & & \\frac{\\partial F_{\\alpha_1}}{\\partial \\alpha_1} & \\\\
F_{\\alpha_2} & & & & & & & & \\frac{\\partial F_{\\alpha_2}}{\\partial \\alpha_2}
\\end{array}
```

This function sets up the indices of these non-zero entries in the sparse Jacobian matrix structure.

# Arguments
- `data::ACPowerFlowData`: The power flow data containing LCC system information.
- `rows::Vector{$J_INDEX_TYPE}`: Vector to store row indices of non-zero Jacobian entries.
- `columns::Vector{$J_INDEX_TYPE}`: Vector to store column indices of non-zero Jacobian entries.
- `values::Vector{Float64}`: Vector to store initial values of non-zero Jacobian entries.
- `num_buses::Int`: Total number of buses in the system.
"""
function _create_jacobian_matrix_structure_lcc(
    data::ACPowerFlowData,
    rows::Vector{J_INDEX_TYPE},
    columns::Vector{J_INDEX_TYPE},
    values::Vector{Float64},
    num_buses::Int,
)
    for (i, (fb, tb)) in enumerate(data.lcc.bus_indices)
        idx_p_fb = 2 * fb - 1
        idx_q_fb = 2 * fb
        idx_p_tb = 2 * tb - 1
        idx_q_tb = 2 * tb
        offset_lcc = num_buses * 2 + (i - 1) * 4
        idx_tap_from = offset_lcc + 1
        idx_tap_to = offset_lcc + 2
        idx_angle_from = offset_lcc + 3
        idx_angle_to = offset_lcc + 4

        rcv = [
            (idx_p_fb, idx_p_fb, 0.0),  # ∂Pᵢ/∂Vᵢ
            (idx_q_fb, idx_p_fb, 0.0),  # ∂Qᵢ/∂Vᵢ
            (idx_p_fb, idx_tap_from, 0.0),  # ∂Pᵢ/∂tᵢ
            (idx_p_fb, idx_angle_from, 0.0),  # ∂Pᵢ/∂αᵢ
            (idx_q_fb, idx_tap_from, 0.0),  # ∂Qᵢ/∂tᵢ
            (idx_q_fb, idx_angle_from, 0.0),  # ∂Qᵢ/∂αᵢ
            (idx_p_tb, idx_p_tb, 0.0),  # ∂Pⱼ/∂Vⱼ
            (idx_q_tb, idx_p_tb, 0.0),  # ∂Qⱼ/∂Vⱼ
            (idx_p_tb, idx_tap_to, 0.0),  # ∂Pⱼ/∂tⱼ
            (idx_p_tb, idx_angle_to, 0.0),  # ∂Pⱼ/∂αⱼ
            (idx_q_tb, idx_tap_to, 0.0),  # ∂Qⱼ/∂tⱼ
            (idx_q_tb, idx_angle_to, 0.0),  # ∂Qⱼ/∂αⱼ
            (idx_tap_from, idx_p_fb, 0.0),  # ∂Fₜᵢ/∂Vᵢ
            (idx_tap_to, idx_p_fb, 0.0),  # ∂Fₜⱼ/∂Vᵢ
            (idx_tap_to, idx_p_tb, 0.0),  # ∂Fₜⱼ/∂Vⱼ
            # Inverter-side slots for the P-setpoint row F_t_fb, used when the
            # set point is at the inverter (F_t_fb = −P_lcc_to − P_set).
            (idx_tap_from, idx_p_tb, 0.0),  # ∂Fₜᵢ/∂Vⱼ
            (idx_tap_from, idx_tap_to, 0.0),  # ∂Fₜᵢ/∂tⱼ
            (idx_tap_from, idx_angle_to, 0.0),  # ∂Fₜᵢ/∂αⱼ
            (idx_tap_from, idx_tap_from, 0.0),  # ∂Fₜᵢ/∂tᵢ
            (idx_tap_from, idx_angle_from, 0.0),  # ∂Fₜᵢ/∂αᵢ
            (idx_tap_to, idx_tap_from, 0.0),  # ∂Fₜⱼ/∂tᵢ
            (idx_tap_to, idx_tap_to, 0.0),  # ∂Fₜⱼ/∂tⱼ
            (idx_tap_to, idx_angle_from, 0.0),  # ∂Fₜⱼ/∂αᵢ
            (idx_tap_to, idx_angle_to, 0.0),  # ∂Fₜⱼ/∂αⱼ
            (idx_angle_from, idx_angle_from, 1.0),  # ∂Fₐᵢ/∂αᵢ
            (idx_angle_to, idx_angle_to, 1.0),  # ∂Fₐⱼ/∂αⱼ
        ]
        for (r, c, v) in rcv
            push!(rows, r)
            push!(columns, c)
            push!(values, v)
        end
    end
    return
end

"""
    _create_jacobian_matrix_structure(data::ACPowerFlowData, time_step::Int64) -> SparseMatrixCSC{Float64, $J_INDEX_TYPE}

Create the structure of the Jacobian matrix for an AC power flow problem.

# Arguments
- `data::ACPowerFlowData`: The power flow model.
- `time_step::Int64`: The specific time step for which the Jacobian matrix structure is created.

# Returns
- `SparseMatrixCSC{Float64, $J_INDEX_TYPE}`: A sparse matrix with structural zeros representing the structure of the Jacobian matrix.

# Description

This function initializes the structure of the Jacobian matrix for an AC power flow problem.
The Jacobian matrix is used in power flow analysis to represent the partial derivatives of bus active and reactive power injections with respect to bus voltage magnitudes and angles.

Unlike some commonly used approaches where the Jacobian matrix is constructed as four submatrices, each grouping values for the four types of partial derivatives,
this function groups the partial derivatives by bus. The structure is organized as groups of 4 values per bus.

This approach is more memory-efficient. Furthermore, this structure results in a more efficient factorization because the values are more likely to be grouped close to the diagonal.
Refer to Electric Energy Systems: Analysis and Operation by Antonio Gomez-Exposito and Fernando L. Alvarado for more details.

The function initializes three arrays (`rows`, `columns`, and `values`) to store the row indices, column indices, and values of the non-zero elements of the Jacobian matrix, respectively.

For each bus in the system, the function iterates over its neighboring buses and determines the type of each neighboring bus (`REF`, `PV`, or `PQ`).
Depending on the bus type, the function adds the appropriate entries to the Jacobian matrix structure.

- For `REF` buses, entries are added for local active and reactive power.
- For `PV` buses, entries are added for active and reactive power with respect to angle, and for local reactive power.
- For `PQ` buses, entries are added for active and reactive power with respect to voltage magnitude and angle.

# Example Structure

For a system with 3 buses where bus 1 is `REF`, bus 2 is `PV`, and bus 3 is `PQ`:

Let ``\\Delta P_j``, ``\\Delta Q_j`` be the active, reactive power balance at the ``j``th bus. Let ``P_j`` and ``Q_j`` be the
active and reactive power generated at the ``j``th bus (`REF` and `PV` only). The state vector is
``x = [P_1, Q_1, Q_2, \\theta_2, V_3, \\theta_3]``, and the residual vector is ``F(x) = [\\Delta P_1, \\Delta Q_1, \\Delta P_2, \\Delta Q_2, \\Delta P_3, \\Delta Q_3]``.

The Jacobian matrix ``J = \\nabla F(x)`` has the structure:

```math
J = \\begin{bmatrix}
\\frac{\\partial \\vec{F}}{\\partial P_1} &
\\frac{\\partial \\vec{F}}{\\partial Q_1} &
\\frac{\\partial \\vec{F}}{\\partial Q_2} &
\\frac{\\partial \\vec{F}}{\\partial \\theta_2} &
\\frac{\\partial \\vec{F}}{\\partial V_3} &
\\frac{\\partial \\vec{F}}{\\partial \\theta_3}
\\end{bmatrix}
```

In reality, for large networks, this matrix would be sparse, and each 2×2 block would only be nonzero
when there's a line between the respective buses.

Finally, the function constructs a sparse matrix from the collected indices and values and returns it.
"""
function _create_jacobian_matrix_structure(
    data::ACPowerFlowData,
    bus_slack_participation_factors::SparseVector{Float64, Int},
    subnetworks::Dict{Int64, Vector{Int64}},
    time_step::Int64,
)
    # Create Jacobian structure
    # Initialize arrays to store the row indices, column indices, and values of the non-zero elements of the Jacobian matrix
    rows = J_INDEX_TYPE[]      # I
    columns = J_INDEX_TYPE[]   # J
    values = Float64[]  # V

    num_buses = first(size(data.bus_type))
    num_lccs = size(data.lcc.p_set, 1)

    num_lines = length(get_arc_lookup(data))
    sizehint!(rows, 4 * num_lines + 15 * num_lccs)
    sizehint!(columns, 4 * num_lines + 15 * num_lccs)
    sizehint!(values, 4 * num_lines + 15 * num_lccs)

    for bus_from in 1:num_buses
        row_from_p = 2 * bus_from - 1  # Row index for the value that is related to active power
        row_from_q = 2 * bus_from      # Row index for the value that is related to reactive power
        for bus_to in data.neighbors[bus_from]
            col_to_vm = 2 * bus_to - 1  # Column index for the value related to voltage magnitude
            col_to_va = 2 * bus_to      # Column index for the value related to voltage angle
            # We ignore the bus type and initialize the structure as if all buses were PQ -
            # mainly because we can have a PV -> PQ transition, and the number of REF buses is small
            # bus_type = data.bus_type[bus_to, time_step]
            _create_jacobian_matrix_structure_bus!(
                rows,
                columns,
                values,
                bus_from,
                bus_to,
                row_from_p,
                row_from_q,
                col_to_vm,
                col_to_va,
                # Val(bus_type),
            )
        end
    end

    # Add structural entries for distributed slack: each participating bus k has
    # ∂F_P_k/∂x[2*ref-1] = -c_k. If bus k is not a neighbor of the ref bus,
    # this entry doesn't exist yet in the sparsity pattern.
    for (ref_bus, subnetwork_buses) in subnetworks
        for bus_k in subnetwork_buses
            bus_slack_participation_factors[bus_k] == 0.0 && continue
            if !(ref_bus in data.neighbors[bus_k])
                push!(rows, J_INDEX_TYPE(2 * bus_k - 1))
                push!(columns, J_INDEX_TYPE(2 * ref_bus - 1))
                push!(values, 0.0)
            end
        end
    end

    _create_jacobian_matrix_structure_lcc(data, rows, columns, values, num_buses)
    _create_jacobian_matrix_structure_vsc(data, rows, columns, values, num_buses)
    _create_jacobian_matrix_structure_area(data, rows, columns, values)
    Jv0 = SparseArrays.sparse(rows, columns, values)
    return Jv0
end

# Structural slots for the VSC tail (polar). Bus×converter injection, the two control rows per
# converter, and the DC-KCL row per node (G_dc pattern + converter coupling). All-zero placeholders;
# filled by `_set_entries_for_vsc`. Bus magnitude column (`2ix-1`) slots carry the AC-voltage / loss
# coupling (zero unless the converter controls |V_ac| or has losses).
function _create_jacobian_matrix_structure_vsc(
    data::ACPowerFlowData,
    rows::Vector{J_INDEX_TYPE},
    columns::Vector{J_INDEX_TYPE},
    values::Vector{Float64},
    num_buses::Int,
)
    dcn = get_dc_network(data)
    has_dc_network(dcn) || return
    nconv = n_vsc_converters(dcn)
    nnode = n_dc_nodes(dcn)
    num_lcc = size(data.lcc.p_set, 1)
    vsc_off = 2 * num_buses + 4 * num_lcc
    base = vsc_off + 2 * nconv
    function push3(r, c)
        push!(rows, J_INDEX_TYPE(r))
        push!(columns, J_INDEX_TYPE(c))
        push!(values, 0.0)
        return
    end
    for c in 1:nconv
        ix = dcn.converter_ac_bus_ix[c]
        k = dcn.converter_dc_node_ix[c]
        pc = vsc_off + 2 * c - 1
        qc = vsc_off + 2 * c
        vk = base + k
        push3(2 * ix - 1, pc)   # ∂P_bal/∂P_c
        push3(2 * ix, qc)       # ∂Q_bal/∂Q_c
        push3(pc, pc)           # ∂r1/∂P_c
        push3(pc, vk)           # ∂r1/∂V_dc
        push3(qc, qc)           # ∂r2/∂Q_c
        push3(qc, 2 * ix - 1)   # ∂r2/∂|V_ac| (Vac modes)
        push3(vk, pc)           # ∂KCL/∂P_c
        push3(vk, qc)           # ∂KCL/∂Q_c (loss)
        push3(vk, 2 * ix - 1)   # ∂KCL/∂|V_ac| (loss)
    end
    for k in 1:nnode
        push3(base + k, base + k)  # DC-KCL diagonal (G_dc[k,k] + converter coupling)
    end
    for b in 1:n_dc_branches(dcn)
        f = dcn.branch_from[b]
        t = dcn.branch_to[b]
        push3(base + f, base + t)  # DC-KCL off-diagonal
        push3(base + t, base + f)
    end
    return
end

# Fill the VSC tail Jacobian entries (polar). Called each iteration after the bus and LCC entries.
function _set_entries_for_vsc(
    data::ACPowerFlowData,
    Jv::SparseArrays.SparseMatrixCSC{Float64, J_INDEX_TYPE},
    num_buses::Int,
    time_step::Int,
)
    dcn = get_dc_network(data)
    has_dc_network(dcn) || return
    nconv = n_vsc_converters(dcn)
    nnode = n_dc_nodes(dcn)
    num_lcc = size(data.lcc.p_set, 1)
    vsc_off = 2 * num_buses + 4 * num_lcc
    base = vsc_off + 2 * nconv
    Vm = view(data.bus_magnitude, :, time_step)
    G = dcn.G_dc
    for b in 1:n_dc_branches(dcn)
        f = dcn.branch_from[b]
        t = dcn.branch_to[b]
        Jv[base + f, base + t] = G[f, t]
        Jv[base + t, base + f] = G[t, f]
    end
    for k in 1:nnode
        Jv[base + k, base + k] = G[k, k]
    end
    # Pre-zero the shared ∂KCL/∂|V_ac| slots before accumulating: two converters can share BOTH
    # the DC node and the AC bus (parallel converters), in which case `sparse` merged their
    # structural slots into one — an `=` write would clobber the first converter's contribution.
    for c in 1:nconv
        if data.bus_type[dcn.converter_ac_bus_ix[c], time_step] == PSY.ACBusTypes.PQ
            Jv[base + dcn.converter_dc_node_ix[c], 2 * dcn.converter_ac_bus_ix[c] - 1] = 0.0
        end
    end
    for c in 1:nconv
        ix = dcn.converter_ac_bus_ix[c]
        k = dcn.converter_dc_node_ix[c]
        pc = vsc_off + 2 * c - 1
        qc = vsc_off + 2 * c
        vk = base + k
        mode = dcn.converter_mode[c]
        Vmix = Vm[ix]
        Vdc = dcn.node_vdc[k, time_step]
        (Pdc, dP, dQ, dVm) = _vsc_pdc_derivatives(dcn, c, Vmix, time_step)
        Jv[2 * ix - 1, pc] = -1.0
        Jv[2 * ix, qc] = -1.0
        Jv[pc, pc] = _vsc_dr1_dP(mode, dcn, c)
        Jv[pc, vk] = _vsc_dr1_dVdc(mode)
        Jv[qc, qc] = _vsc_dr2_dQ(mode)
        Jv[vk, pc] = dP / Vdc
        Jv[vk, qc] = dQ / Vdc
        Jv[vk, vk] += -Pdc / (Vdc * Vdc)
        # Column `2ix-1` is the |V_ac| state only at PQ buses; at PV/REF |V_ac| is fixed, so the
        # converter's |V_ac|-coupling derivatives (AC-voltage control + loss) do not enter the
        # Jacobian. The structure allocates the slot as PQ regardless (PV→PQ transitions), so
        # leaving it unwritten here keeps a correct structural zero.
        if data.bus_type[ix, time_step] == PSY.ACBusTypes.PQ
            Jv[qc, 2 * ix - 1] = _vsc_dr2_dVm(mode, Vmix)
            Jv[vk, 2 * ix - 1] += dVm / Vdc
        end
    end
    return
end

function _set_entries_for_lcc(data::ACPowerFlowData,
    Jv::SparseArrays.SparseMatrixCSC{Float64, J_INDEX_TYPE},
    num_buses::Int,
    time_step::Int)
    for (i, (fb, tb)) in enumerate(data.lcc.bus_indices)
        idx_p_fb = 2 * fb - 1
        idx_q_fb = 2 * fb
        idx_p_tb = 2 * tb - 1
        idx_q_tb = 2 * tb
        offset_lcc = num_buses * 2 + (i - 1) * 4
        idx_tap_from = offset_lcc + 1
        idx_tap_to = offset_lcc + 2
        idx_angle_from = offset_lcc + 3
        idx_angle_to = offset_lcc + 4

        # F_α = α − α_min has a constant unit self-derivative; write it each iteration so
        # every nonzero is owned by the update path, not seeded only at construction.
        Jv[idx_angle_from, idx_angle_from] = 1.0
        Jv[idx_angle_to, idx_angle_to] = 1.0

        alpha_r = data.lcc.rectifier.thyristor_angle[i, time_step]
        alpha_i = data.lcc.inverter.thyristor_angle[i, time_step]
        phi_r = data.lcc.rectifier.phi[i, time_step]
        phi_i = data.lcc.inverter.phi[i, time_step]
        xtr_r = data.lcc.rectifier.transformer_reactance[i]
        xtr_i = data.lcc.inverter.transformer_reactance[i]
        Vm_fb = data.bus_magnitude[fb, time_step]
        Vm_tb = data.bus_magnitude[tb, time_step]
        bus_type_fb = data.bus_type[fb, time_step]
        bus_type_tb = data.bus_type[tb, time_step]

        if iszero(data.lcc.i_dc[i, time_step])
            # 0-current converter: P_lcc ≡ 0, so it contributes nothing to the bus
            # rows and its P-setpoint / DC-line-balance rows are vacuous. Zero its
            # bus-coupling entries and pin the two tap states with identity rows
            # (matching _write_lcc_tail!), keeping the block nonsingular without
            # changing the sparsity structure.
            Jv[idx_p_fb, idx_tap_from] = 0.0
            Jv[idx_p_fb, idx_angle_from] = 0.0
            Jv[idx_q_fb, idx_tap_from] = 0.0
            Jv[idx_q_fb, idx_angle_from] = 0.0
            Jv[idx_p_tb, idx_tap_to] = 0.0
            Jv[idx_p_tb, idx_angle_to] = 0.0
            Jv[idx_q_tb, idx_tap_to] = 0.0
            Jv[idx_q_tb, idx_angle_to] = 0.0
            if bus_type_fb == PSY.ACBusTypes.PQ
                Jv[idx_tap_from, idx_p_fb] = 0.0
                Jv[idx_tap_to, idx_p_fb] = 0.0
            end
            if bus_type_tb == PSY.ACBusTypes.PQ
                Jv[idx_tap_from, idx_p_tb] = 0.0
                Jv[idx_tap_to, idx_p_tb] = 0.0
            end
            Jv[idx_tap_from, idx_tap_from] = 1.0   # ∂(tap_r − tap_set)/∂tap_r
            Jv[idx_tap_from, idx_angle_from] = 0.0
            Jv[idx_tap_from, idx_tap_to] = 0.0
            Jv[idx_tap_from, idx_angle_to] = 0.0
            Jv[idx_tap_to, idx_tap_from] = 0.0
            Jv[idx_tap_to, idx_tap_to] = 1.0       # ∂(tap_i − tap_set)/∂tap_i
            Jv[idx_tap_to, idx_angle_from] = 0.0
            Jv[idx_tap_to, idx_angle_to] = 0.0
            continue
        end

        s = _lcc_jacobian_scalars(data, i, time_step, Vm_fb, Vm_tb)

        dP_dV_fb = s.dP_dV_fb
        dP_dV_tb = s.dP_dV_tb
        dP_dt_fb = s.dP_dt_fb
        dP_dt_tb = s.dP_dt_tb

        # Bus-row × tail-column entries (∂{P,Q}/∂{tap, α}) are written
        # unconditionally — the bus residual rows exist for all bus types,
        # and tap/α are state variables regardless of which AC terminal is
        # PQ/PV/REF. ∂{P,Q}/∂V is gated by PQ (V is a state only there);
        # likewise the tail × bus-V chain rule.
        Jv[idx_p_fb, idx_tap_from] = dP_dt_fb # ∂P_fb/∂t_fb
        Jv[idx_p_fb, idx_angle_from] = s.dP_dα_fb # ∂P_fb/∂α_fb
        Jv[idx_q_fb, idx_tap_from] =
            _calculate_dQ_dt_lcc(s.tap_r, s.i_dc, xtr_r, Vm_fb, phi_r) # ∂Q_fb/∂t_fb
        Jv[idx_q_fb, idx_angle_from] =
            _calculate_dQ_dα_lcc(s.tap_r, s.i_dc, xtr_r, Vm_fb, phi_r, alpha_r) # ∂Q_fb/∂α_fb
        Jv[idx_p_tb, idx_tap_to] = dP_dt_tb # ∂P_tb/∂t_tb
        Jv[idx_p_tb, idx_angle_to] = s.dP_dα_tb # ∂P_tb/∂α_tb
        # Inverter dQ: −xtr_i flips the commutation-chain term to the inverter sign
        # (see _lcc_jacobian_scalars); the leading sin ϕ_i term is x_t-free.
        Jv[idx_q_tb, idx_tap_to] =
            _calculate_dQ_dt_lcc(s.tap_i, s.i_dc, -xtr_i, Vm_tb, phi_i) # ∂Q_tb/∂t_tb
        # φ_i convention flips sign of ∂φ_i/∂α_i vs the rectifier; negate helper output.
        Jv[idx_q_tb, idx_angle_to] =
            -_calculate_dQ_dα_lcc(s.tap_i, s.i_dc, xtr_i, Vm_tb, phi_i, alpha_i) # ∂Q_tb/∂α_tb

        if bus_type_fb == PSY.ACBusTypes.PQ
            Jv[idx_p_fb, idx_p_fb] += dP_dV_fb # ∂P_fb/∂V_fb
            Jv[idx_q_fb, idx_p_fb] +=
                _calculate_dQ_dV_lcc(s.tap_r, s.i_dc, xtr_r, Vm_fb, phi_r) # ∂Q_fb/∂V_fb
            # ∂F_t_fb/∂V_fb is nonzero only with a rectifier-side set point;
            # the scalar is pre-zeroed otherwise.
            Jv[idx_tap_from, idx_p_fb] = s.d_Ft_fb_d_V_fb # ∂F_t_fb/∂V_fb
            Jv[idx_tap_to, idx_p_fb] = dP_dV_fb # ∂F_t_tb/∂V_fb
        end

        if bus_type_tb == PSY.ACBusTypes.PQ
            Jv[idx_p_tb, idx_p_tb] += dP_dV_tb # ∂P_tb/∂V_tb
            Jv[idx_q_tb, idx_p_tb] +=
                _calculate_dQ_dV_lcc(s.tap_i, s.i_dc, -xtr_i, Vm_tb, phi_i) # ∂Q_tb/∂V_tb (−xtr_i: inverter commutation sign)
            # ∂F_t_fb/∂V_tb is nonzero only with an inverter-side set point.
            Jv[idx_tap_from, idx_p_tb] = s.d_Ft_fb_d_V_tb # ∂F_t_fb/∂V_tb
            Jv[idx_tap_to, idx_p_tb] = dP_dV_tb # ∂F_t_tb/∂V_tb
        end

        # P-setpoint row F_t_fb: rectifier-side (tap_r, α_r) and inverter-side
        # (tap_i, α_i) slots are written unconditionally; the scalars helper
        # zeroes whichever side the set point is not on.
        Jv[idx_tap_from, idx_tap_from] = s.d_Ft_fb_d_tap_r
        Jv[idx_tap_from, idx_angle_from] = s.d_Ft_fb_d_alpha_r
        Jv[idx_tap_from, idx_tap_to] = s.d_Ft_fb_d_tap_i
        Jv[idx_tap_from, idx_angle_to] = s.d_Ft_fb_d_alpha_i
        Jv[idx_tap_to, idx_tap_from] = s.d_Ft_tb_d_tap_r
        Jv[idx_tap_to, idx_tap_to] = s.d_Ft_tb_d_tap_i
        Jv[idx_tap_to, idx_angle_from] = s.d_Ft_tb_d_alpha_r
        Jv[idx_tap_to, idx_angle_to] = s.d_Ft_tb_d_alpha_i
    end
    return
end

"""Bus indices of REF buses sharing an island with another REF (multi-swing). Each
self-balances its own P-slot (`∂F_P/∂x[2i−1] = −1`) instead of the distributed island
scalar; single-swing islands are excluded and keep the distributed-slack path."""
function _multi_swing_ref_indices(
    bus_type::AbstractMatrix{PSY.ACBusTypes},
    subnetworks::Dict{Int64, Vector{Int64}},
    time_step::Int64,
)
    independent = Set{Int}()
    for subnetwork_buses in values(subnetworks)
        refs = filter(ix -> bus_type[ix, time_step] == PSY.ACBusTypes.REF, subnetwork_buses)
        length(refs) > 1 && union!(independent, refs)
    end
    return independent
end

"""Update Jv from the bus voltages/angles in `data`.

INVARIANT (Phase 3 depends on this): every structural nonzero produced by the
Ybus sweep is written on every call — including the slots that are genuinely 0
for PV/REF neighbors and the constant REF/PV diagonal-block entries the old fill
only set at construction. The hot path writes `nonzeros(Jv)` through the
construction-time offset caches (`od_jnz`, `diag_jnz`); no setindex/getindex on
sparse matrices and exactly one `sincos(θ_from − θ_to)` per directed Ybus edge.
Slack cross-terms and LCC tail entries are small, structural-only sets and stay
on the existing setindex path."""
function _update_jacobian_matrix_values!(
    Jv::SparseArrays.SparseMatrixCSC{Float64, J_INDEX_TYPE},
    data::ACPowerFlowData,
    time_step::Int64,
    bus_slack_participation_factors::SparseVector{Float64, Int},
    subnetworks::Dict{Int64, Vector{Int64}},
    independent_ref::Set{Int},
    bus_active_constant_I::Vector{Float64},
    bus_reactive_constant_I::Vector{Float64},
    bus_active_constant_Z::Vector{Float64},
    bus_reactive_constant_Z::Vector{Float64},
    od_from::Vector{Int},
    od_to::Vector{Int},
    od_ybus_nz::Vector{Int},
    od_jnz::Matrix{Int},
    diag_jnz::Matrix{Int},
    diag_ybus_nz::Vector{Int},
    diag_accum::Matrix{Float64},
)
    Yb = data.power_network_matrix.data
    Yb_vals = SparseArrays.nonzeros(Yb)
    Jvnz = SparseArrays.nonzeros(Jv)
    Vm = view(data.bus_magnitude, :, time_step)
    θ = view(data.bus_angles, :, time_step)
    bus_types = view(data.bus_type, :, time_step)
    num_buses = first(size(data.bus_type))

    fill!(diag_accum, 0.0)

    # Off-diagonal sweep. diag_accum rows mirror the old `diag_elements`:
    # 1: ∂P∂θ_from, 2: ∂Q∂θ_from, 3: ∂P∂V_from, 4: ∂Q∂V_from.
    @inbounds for e in eachindex(od_from)
        bus_from = od_from[e]
        bus_to = od_to[e]
        y = Yb_vals[od_ybus_nz[e]]
        g_ij = real(y)
        b_ij = imag(y)
        Vm_from = Vm[bus_from]
        Vm_to = Vm[bus_to]
        sinθ, cosθ = sincos(θ[bus_from] - θ[bus_to])
        p_vm_common = g_ij * cosθ + b_ij * sinθ
        q_vm_common = g_ij * sinθ - b_ij * cosθ
        p_va_common = Vm_from * Vm_to * q_vm_common          # Vm_f·Vm_t·(g·sin − b·cos)
        q_va_common = Vm_from * Vm_to * (-g_ij * cosθ - b_ij * sinθ)
        # Diagonal accumulation is bus_to-type-independent (REF/PV/PQ identical).
        diag_accum[3, bus_from] += Vm_to * p_vm_common
        diag_accum[1, bus_from] -= p_va_common
        diag_accum[4, bus_from] += Vm_to * q_vm_common
        diag_accum[2, bus_from] -= q_va_common
        # Off-diagonal slot values depend on bus_to type: PQ writes all four; PV
        # zeroes the (·, Vm) columns (Vm_to not a state); REF zeroes all four
        # (its columns hold P_gen/Q_gen). Every slot is written each call.
        bt = bus_types[bus_to]
        if bt == PSY.ACBusTypes.PQ
            Jvnz[od_jnz[1, e]] = Vm_from * p_vm_common  # Jv[p, vm]
            Jvnz[od_jnz[2, e]] = Vm_from * q_vm_common  # Jv[q, vm]
            Jvnz[od_jnz[3, e]] = p_va_common            # Jv[p, va]
            Jvnz[od_jnz[4, e]] = q_va_common            # Jv[q, va]
        elseif bt == PSY.ACBusTypes.PV
            Jvnz[od_jnz[1, e]] = 0.0
            Jvnz[od_jnz[2, e]] = 0.0
            Jvnz[od_jnz[3, e]] = p_va_common
            Jvnz[od_jnz[4, e]] = q_va_common
        else  # REF
            Jvnz[od_jnz[1, e]] = 0.0
            Jvnz[od_jnz[2, e]] = 0.0
            Jvnz[od_jnz[3, e]] = 0.0
            Jvnz[od_jnz[4, e]] = 0.0
        end
    end

    # Diagonal blocks. diag_jnz rows: 1: Jv[p, vm], 2: Jv[q, vm], 3: Jv[p, va],
    # 4: Jv[q, va]. diag_accum slot 3→Jv[p,vm], 4→Jv[q,vm], 1→Jv[p,va], 2→Jv[q,va].
    @inbounds for bus_from in 1:num_buses
        Vm_from = Vm[bus_from]
        bt = bus_types[bus_from]
        if bt == PSY.ACBusTypes.PQ
            Jvnz[diag_jnz[3, bus_from]] = diag_accum[1, bus_from]  # ∂P∂θ_from
            Jvnz[diag_jnz[4, bus_from]] = diag_accum[2, bus_from]  # ∂Q∂θ_from
            yii = if diag_ybus_nz[bus_from] == 0
                zero(eltype(Yb_vals))
            else
                Yb_vals[diag_ybus_nz[bus_from]]
            end
            d3 = diag_accum[3, bus_from] + 2 * real(yii) * Vm_from  # ∂P∂V_from
            d4 = diag_accum[4, bus_from] - 2 * imag(yii) * Vm_from  # ∂Q∂V_from
            # ZIP chain rule: P_net(V) = P₀ − const_I_P·V − const_Z_P·V², so ∂F_P/∂V
            # picks up −∂P_net/∂V = +const_I_P + 2·const_Z_P·V (same shape on Q).
            d3 +=
                bus_active_constant_I[bus_from] +
                2 * bus_active_constant_Z[bus_from] * Vm_from
            d4 +=
                bus_reactive_constant_I[bus_from] +
                2 * bus_reactive_constant_Z[bus_from] * Vm_from
            Jvnz[diag_jnz[1, bus_from]] = d3  # ∂P∂V_from
            Jvnz[diag_jnz[2, bus_from]] = d4  # ∂Q∂V_from
        elseif bt == PSY.ACBusTypes.PV
            Jvnz[diag_jnz[1, bus_from]] = 0.0
            Jvnz[diag_jnz[2, bus_from]] = -1.0
            Jvnz[diag_jnz[3, bus_from]] = diag_accum[1, bus_from]  # ∂P∂θ_from
            Jvnz[diag_jnz[4, bus_from]] = diag_accum[2, bus_from]  # ∂Q∂θ_from
        else  # REF
            if bus_from in independent_ref
                # Multi-swing island: this swing self-balances at its own P-slot, so
                # ∂F_P/∂x[2i−1] = −1 (not the distributed −c_ref).
                Jvnz[diag_jnz[1, bus_from]] = -1.0
            else
                Jvnz[diag_jnz[1, bus_from]] = -bus_slack_participation_factors[bus_from]
            end
            Jvnz[diag_jnz[2, bus_from]] = 0.0
            Jvnz[diag_jnz[3, bus_from]] = 0.0
            Jvnz[diag_jnz[4, bus_from]] = -1.0
        end
    end

    # Distributed slack cross-terms: for each participating bus k (other than the
    # REF bus), the active power residual depends on the REF bus state variable
    # x[2*ref-1] through the slack distribution: ∂F_P_k/∂x[2*ref-1] = -c_k.
    for (ref_bus, subnetwork_buses) in subnetworks
        # Multi-swing island: each swing self-balances at its own P-slot (handled in the
        # per-bus diagonal fill above); there is no single distributed scalar to couple, so
        # skip the cross-terms entirely.
        ref_bus in independent_ref && continue
        col_ref = 2 * ref_bus - 1
        for bus_k in subnetwork_buses
            bus_k == ref_bus && continue
            c_k = bus_slack_participation_factors[bus_k]
            c_k == 0.0 && continue
            Jv[2 * bus_k - 1, col_ref] = -c_k
        end
    end

    _set_entries_for_lcc(data, Jv, num_buses, time_step)
    _set_entries_for_vsc(data, Jv, num_buses, time_step)
    _set_entries_for_area(data, Jv, time_step)
    return
end

"""
    calculate_loss_factors(data::ACPowerFlowData, Jv::SparseMatrixCSC{Float64, $J_INDEX_TYPE}, time_step::Int)

Calculate and store the active power loss factors in the `loss_factors` matrix of the `ACPowerFlowData` structure for a given time step.

The loss factors are computed using the Jacobian matrix `Jv` and the vector `dSbus_dV_ref`, which contains the
partial derivatives of slack power with respect to bus voltages. The function interprets changes in
slack active power injections as indicative of changes in grid active power losses.
KLU is used to factorize the sparse Jacobian matrix to solve for the loss factors.

# Arguments
- `data::ACPowerFlowData`: The data structure containing power flow information, including the `loss_factors` matrix.
- `Jv::SparseMatrixCSC{Float64, $J_INDEX_TYPE}`: The sparse Jacobian matrix of the power flow system.
- `time_step::Int`: The time step index for which the loss factors are calculated.
"""
function _calculate_loss_factors(
    data::ACPowerFlowData,
    Jv::SparseMatrixCSC{Float64, J_INDEX_TYPE},
    time_step::Int,
)
    bus_numbers = 1:first(size(data.bus_type))
    ref_mask = data.bus_type[:, time_step] .== (PSY.ACBusTypes.REF,)
    if count(ref_mask) > 1
        error(
            "Loss factors with multiple REF buses isn't supported.",
        )
    end
    pvpq_mask = .!ref_mask
    ref = findfirst(ref_mask)
    new_ref_mask = falses(size(ref_mask))
    new_ref_mask[ref] = true
    pvpq_mask = .!(new_ref_mask)
    pvpq_coord_mask = repeat(pvpq_mask; inner = 2)
    J_t = sparse(transpose(Jv[pvpq_coord_mask, pvpq_coord_mask]))
    dSbus_dV_ref = collect(Jv[2 .* ref .- 1, pvpq_coord_mask])[:]
    lf_cache = make_linear_solver_cache(PNM.KLUSolver(), J_t)
    full_factor!(lf_cache, J_t)
    lf = copy(dSbus_dV_ref)
    solve!(lf_cache, lf)
    # only take the dPref_dP loss factors, ignore dPref_dQ
    data.loss_factors[pvpq_mask, time_step] .= lf[1:2:end]
    data.loss_factors[new_ref_mask, time_step] .= -1.0
end

"""
    calculate_voltage_stability_factors(data::ACPowerFlowData, J::ACPowerFlowJacobian, time_step::Integer)

Calculate and store the voltage stability factors in the `voltage_stability_factors` matrix of the `ACPowerFlowData` structure for a given time step.
The voltage stability factors are computed using the Jacobian matrix `J` in block format after a converged power flow calculation.
The results are stored in the `voltage_stability_factors` matrix in the `data` instance.
The factor for the grid as a whole (σ) is stored in the position of the REF bus.
The values of the singular vector `v` indicate the sensitivity of the buses and are stored in the positions of the PQ buses.
The values of `v` for PV buses are set to zero.
The function uses the method described in \"Fast calculation of a voltage stability index\" by PA Lof et. al.
# Arguments
- `data::ACPowerFlowData`: The instance containing the grid model data.
- `J::ACPowerFlowJacobian`: The Jacobian matrix cache.
- `time_step::Integer`: The calculated time step.
"""
function _calculate_voltage_stability_factors(
    data::ACPowerFlowData,
    Jv::SparseMatrixCSC{Float64, J_INDEX_TYPE},
    time_step::Integer,
)
    ref, pv, pq = bus_type_idx(data, time_step)
    pvpq = [pv; pq]
    rows, cols = _block_J_indices(pvpq, pq)
    σ, _, right = _singular_value_decomposition(Jv[rows, cols], length(pvpq))
    # Store σ at REF bus, set remaining REF buses (if any) to zero
    data.voltage_stability_factors[first(ref), time_step] = σ
    data.voltage_stability_factors[ref[2:end], time_step] .= 0.0
    # PV buses have zero sensitivity, PQ buses get the right singular vector
    data.voltage_stability_factors[pv, time_step] .= 0.0
    data.voltage_stability_factors[pq, time_step] .= right
    return
end

"""
    _block_J_indices(data::ACPowerFlowData, time_step::Int) -> (Vector{$J_INDEX_TYPE}, Vector{$J_INDEX_TYPE})

Get the indices to reindex the Jacobian matrix from the interleaved form to the block form:

```math
\\begin{bmatrix}
\\frac{\\partial P}{\\partial \\theta} & \\frac{\\partial P}{\\partial V} \\\\
\\frac{\\partial Q}{\\partial \\theta} & \\frac{\\partial Q}{\\partial V}
\\end{bmatrix}
```

# Arguments
- `pvpq::Vector{$J_INDEX_TYPE}`: Indices of the buses that are PV or PQ buses.
- `pq::Vector{$J_INDEX_TYPE}`: Indices of the buses that are PQ buses.

# Returns
- `rows::Vector{$J_INDEX_TYPE}`: Row indices for the block Jacobian matrix.
- `cols::Vector{$J_INDEX_TYPE}`: Column indices for the block Jacobian matrix.
"""
function _block_J_indices(pvpq::Vector{<:Integer}, pq::Vector{<:Integer})
    rows = vcat(2 .* pvpq .- 1, 2 .* pq)
    cols = vcat(2 .* pvpq, 2 .* pq .- 1)

    return rows, cols
end

"""
    _singular_value_decomposition(J::SparseMatrixCSC{Float64, $J_INDEX_TYPE}, npvpq::Integer; tol::Float64 = 1e-9, max_iter::Integer = 100,)

Estimate the smallest singular value `σ` and corresponding left and right singular vectors `u` and `v` of a sparse matrix `G_s` (a sub-matrix of `J`).
This function uses an iterative method involving LU factorization of the Jacobian matrix to estimate the smallest singular value of `G_s`.
The algorithm alternates between updating `u` and `v`, normalizing, and checking for convergence based on the change in the estimated singular value `σ`.
The function uses the method described in `Algorithm 3` of \"Fast calculation of a voltage stability index\" by PA Lof et. al.

# Arguments
- `J::SparseMatrixCSC{Float64, $J_INDEX_TYPE}`: The sparse block-form Jacobian matrix.
- `npvpq::Integer`: Number of PV and PQ buses in J.

# Keyword Arguments
- `tol::Float64=1e-9`: Convergence tolerance for the iterative algorithm.
- `max_iter::Integer=100`: Maximum number of iterations.

# Returns
- `σ::Float64`: The estimated smallest singular value.
- `left::Vector{Float64}`: The estimated left singular vector (referred to as `u` in the cited paper).
- `right::Vector{Float64}`: The estimated right singular vector (referred to as `v` in the cited paper).
"""
function _singular_value_decomposition(
    Jv::SparseMatrixCSC{Float64, J_INDEX_TYPE},
    npvpq::Integer;
    tol::Float64 = 1e-9,
    max_iter::Integer = 100,
)
    # Voltage-stability factors solve `Aᵀ x = b` reusing the existing factorization of
    # `A` (rather than factoring `Aᵀ` separately). Only the KLU backend exposes that
    # transposed-solve-from-an-A-factorization, so this routine is KLU-only by construction.
    factorized_block_J = make_linear_solver_cache(PNM.KLUSolver(), Jv)
    full_factor!(factorized_block_J, Jv)
    n = size(Jv, 1)
    voltage_angle_indices = 1:npvpq

    right = ones(n)
    right_angle_section = view(right, voltage_angle_indices)
    fill!(right_angle_section, 0.0)  # Set the part of `right` corresponding to voltage angles to zero
    right ./= norm(right, 2)

    left = ones(n)
    left_angle_section = view(left, voltage_angle_indices)
    fill!(left_angle_section, 0.0)  # Set the part of `left` corresponding to voltage angles to zero

    σ = 1e6  # min. singular value
    k = 1

    while k <= max_iter
        copyto!(left, right)
        tsolve!(factorized_block_J, left)
        fill!(left_angle_section, 0.0)
        norm_left = norm(left, 2)

        σ_1 = 1 / norm_left
        delta_σ = σ_1 - σ
        σ = σ_1

        ldiv!(left, norm_left, left)

        if abs(delta_σ) < tol
            break
        end

        copyto!(right, left)
        solve!(factorized_block_J, right)
        fill!(right_angle_section, 0.0)
        norm_right = norm(right, 2)

        σ_2 = 1 / norm_right
        delta_σ = σ_2 - σ
        σ = σ_2

        ldiv!(right, norm_right, right)

        if abs(delta_σ) < tol
            break
        end

        k += 1
    end
    return σ, left[(npvpq + 1):end], right[(npvpq + 1):end]
end
